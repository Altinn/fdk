prefix apf: <http://jena.apache.org/ARQ/property#>
prefix dcat: <http://www.w3.org/ns/dcat#>
prefix dct:  <https://purl.org/dc/terms/>
prefix : <http://data.brreg.no/vocab/temp/> 
prefix foaf:  <http://xmlns.com/foaf/0.1/>
prefix adms: <http://www.w3.org/ns/adms#> 
prefix dct: <http://purl.org/dc/terms/>
prefix time: <http://www.w3.org/TR/owl-time/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix vcard: <http://www.w3.org/2006/vcard/ns#> 
prefix dcatno: <http://difi.no/dcatno#>  
prefix prov: <http://www.w3.org/ns/prov#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix gn: <http://www.geonames.org/ontology#>
prefix skos: <http://www.w3c.org/2004/02/skos/core#> 


# catalog
construct {
    ?id a dcat:Catalog .
    ?id dct:title ?catalogTitle .
    ?id dct:description ?catalogDescription .
    ?id dct:publisher ?catalogPublisher .
    ?id dcat:dataset ?datasetIRI .
    ?id foaf:homepage ?homepageIRI .
}

WHERE {
    ?id a dcat:Catalog .

     # catalog title
     OPTIONAL {
        ?id :tittel ?catalogTitle .
     }
  
     # catalog description
     OPTIONAL {
        ?id :beskrivelse ?catalogDescription .
     }
     
     # catalog publisher
     OPTIONAL {
        ?id :utgiver ?catalogPublisher .
     }

     # organisation number
     OPTIONAL {
        ?id :orgnr ?organisationNumber .
     }
       
     # split :datasets into multiple dataset
     OPTIONAL {
        ?id :datasets ?datasets .
        ?dataset apf:strSplit(?datasets ",") .
     }

     # create IRI of dataset, assume exists
     BIND (IF(?dataset, IRI(CONCAT("http://data.brreg.no/datakatalog/dataset/", ?organisationNumber, "/", ?dataset)), ?unbound)  AS ?datasetIRI)
     
   
     # create IRI of homepage
     OPTIONAL {
        ?id :homepage ?homepage .
        BIND(IRI(?homepage) AS ?homepageIRI)
     }
}

