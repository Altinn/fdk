prefix apf: <http://jena.apache.org/ARQ/property#>
prefix dcat: <http://www.w3.org/ns/dcat#>
prefix dct:  <https://purl.org/dc/terms/>
prefix : <http://data.brreg.no/vocab/temp/> 
prefix foaf:  <http://xmlns.com/foaf/0.1/>
prefix adms: <http://www.w3.org/ns/adms#> 
prefix dct: <http://purl.org/dc/terms/>
prefix time: <http://www.w3.org/TR/owl-time/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix vcard: <http://www.w3.org/2006/vcard/ns#> 
prefix dcatno: <http://difi.no/dcatno#>  
prefix prov: <http://www.w3.org/ns/prov#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix gn: <http://www.geonames.org/ontology#>
prefix skos: <http://www.w3c.org/2004/02/skos/core#> 

construct {
    ?s ?p ?o . # just to make sure we get it all...
    #?id a ?class .
    
 # dataset   
    ?s dct:identifier ?identifier .
    ?s dct:title ?title_nb .
    ?s dct:title ?title_en .
    ?s dct:description ?description_nb .
    ?s dct:description ?description_en .
    ?s dct:publisher ?publisher .
    ?s dcat:theme ?theme .
    ?s dct:provenance ?provenanceIRI .
    ?s dcat:keyword ?keyword_no .
    ?s dct:subject ?begrepIRI .
    ?s dct:accessRights ?accessRights .
    ?s dct:spatial ?spatialIRI .
    ?s dcatno:accessRightsComment ?skjermingshjemmelIRI .
    ?s dct:accrualPeriodicity ?accrualPeriodicity .
    ?s dcat:contactPoint ?contactPoint .
    ?s dct:conformsTo ?conformsToIRI .
    ?s foaf:page ?pageIRI .
    ?s adms:sample ?sampleIRI .
    ?s dct:source ?source .
    ?s dct:landingPage ?landingPageIRI .
    ?s dct:language ?language .
    ?s adms:identifier ?alternativeIdentifier .
    ?s dct:issued ?issued .
    ?s dct:modified ?modified .
    ?s dcat:distribution ?distribution .

# catalog
    ?s dct:title ?catalogTitle .
    ?s dct:description ?catalogDescription .
    ?s dct:publisher ?catalogPublisher .
    ?s dcat:dataset ?datasetIRI .
    ?s foaf:homepage ?homepageIRI .

# publisher
    ?s dct:identifier ?publisherIdentifier .
    ?s foaf:name ?publisherName .
    
# contact
    ?s vcard:organization-unit ?organization_unit .
    ?s vcard:hasEmail ?hasEmail .
    ?s vcard:hasTelephone ?hasTelephone .

# opphav
    ?s prov:wasGeneratedBy ?wasGeneratedBy .
    ?s rdfs:label ?wasGeneratedBy .
    ?s rdfs:comment ?provenanceComment .

# location
    ?s gn:name ?gn_name .
    ?s gn:officialName ?gn_officialName_no .
    ?s gn:officialName ?gn_officialName_en .
    ?s a ?gnFeatureClass .

# access rights
    ?s skos:prefLabel ?accessRightNo .
    ?s skos:prefLabel ?accessRightEn .
}



WHERE {
     # get all triples
     ?id ?p ?o .
     
     
     
     # clean out properties from our own namespace
     FILTER (regex(str(?p), "^(?!http://data.brreg.no).+"))
    
     # also clean out properties when in subject from our own namespace
     FILTER (regex(str(?id), "^(?!http://data.brreg.no/vocab).+"))

     # add identifier (ok)
     OPTIONAL {?id :identifikator ?identifier .}

     # add language to title (ok)
     OPTIONAL {?id :tittel_no ?title .
     BIND (STRLANG(?title, "nb") as ?title_nb)}
     
     # add language to english title (ok)
     OPTIONAL {?id :tittel_en ?title2 .
     BIND (STRLANG(?title2, "en") as ?title_en)}

     # add language to norwegian description (ok)
     OPTIONAL {?id :beskrivelse_no ?description .
     BIND (STRLANG(?description, "nb") as ?description_nb)}

     # add language to english description (ok)
     OPTIONAL {?id :beskrivelse_en ?description2 .
     BIND (STRLANG(?description2, "en") as ?description_en)}

     # add publisher (ok)
     OPTIONAL {?id :utgiver ?publisher .} 

     # add theme (ok)
     OPTIONAL {?id :tema ?theme .}

     # create IRI of provenance, if exists
     OPTIONAL {?id :provinence ?provenance} . 
     BIND (IRI(?provenance) AS ?provenanceIRI)
    
     # split :keywords into multiple keyword (ok)
     OPTIONAL {?id :keywords ?keywords .
     ?keyword apf:strSplit(?keywords ",") 
     BIND (STRLANG(?keyword, "nb") as ?keyword_no)}
     
     # create IRI of :begrep, if exsist (ok)
     OPTIONAL {?id :begrep ?begrep} .
     BIND (IRI(?begrep) AS ?begrepIRI)
     
     # create IRI of spatial, if exists (ok)    
     OPTIONAL {?id :spatial ?spatial} .
     BIND (IRI(?spatial) AS ?spatialIRI)
     
     # add accessRights (ok)
     OPTIONAL {?id :tilgangsnivå ?accessRights . }

     # create IRI of :skjermingshjemmel, if exsist (ok)
     OPTIONAL {?id :skjermingshjemmel ?skjermingshjemmel} .
     BIND (IRI(?skjermingshjemmel) AS ?skjermingshjemmelIRI)

     # add accrualPeriodicity (ok)
     OPTIONAL {?id :frekvens ?accrualPeriodicity. }

     # add contactPoint (ok)
     OPTIONAL {?id :kontaktpunkt ?contactPoint . }
     
     # create IRI of conformsTo, if exists (ok)
     OPTIONAL {?id :iSamsvarMed ?conformsTo .
     BIND (IRI(?conformsTo) AS ?conformsToIRI)}
     
     # create IRI of page, if exists  (ok)
     OPTIONAL {?id :dokumentasjon ?page} .
     BIND (IRI(?page) AS ?pageIRI)    
 
     # create IRI of sample, if exists (ok)
     OPTIONAL {?id :eksempel ?sample} . 
     BIND (IRI(?sample) AS ?sampleIRI)
    
     # create IRI of landingpage, if exists
     OPTIONAL {?id :landingsside ?landingpage} .
     BIND (IRI(?landingpage) AS ?landingPageIRI)
     
     # add language
     OPTIONAL {?id :språk ?language .}
 
     # add alternative identifier
     OPTIONAL {?id :alternativIdentifikator ?alternativeIdentifier . }
    
     # add issued  - convert to xsd:Date
     OPTIONAL {?id :utgivelsesDato ?issuedDate .
     BIND(xsd:date(str(?issuedDate)) as ?issued)}
    
     # add modified 
     OPTIONAL {?id :modifisert ?modified .}
    
     # add distribution
     OPTIONAL {?id :distribusjon ?distribution.}
    
     # add source
     OPTIONAL {?id :kilde ?source . } 

# Catalog

     # catalog title
     OPTIONAL {?id :tittel ?catalogTitle .
     ?id a dcat:Catalog .}
  
     # catalog description
     OPTIONAL {?id :beskrivelse ?catalogDescription .
     ?id a dcat:Catalog .}
     
     # catalog publisher
     OPTIONAL {?id :utgiver ?catalogPublisher .
     ?id a dcat:Catalog .}
       
     # split :datasets into multiple dataset
     OPTIONAL {?id :datasets ?datasets .
     ?dataset apf:strSplit(?datasets ",")}
     # create IRI of dataset, assume exists
     
     #OPTIONAL {
        BIND (IF(?dataset, IRI(CONCAT("http://data.brreg.no/datakatalog/dataset/", ?dataset)), ?unbound)  AS ?datasetIRI)
     #   FILTER(BOUND(?dataset))
     #}
   
     # create IRI of homepage
     OPTIONAL {?id :homepage ?homepage} .
     BIND(IRI(?homepage) AS ?homepageIRI)


# Publisher
 
     # identifier
     OPTIONAL {
     ?id :organisasjonsnummer ?publisherIdentifier .
     ?id :navn ?publisherName .
     }
     
# Contact point     
     
     OPTIONAL {?id :avdelingsnavn ?organization_unit .
        ?id a vcard:Organization . 
     }
     
     # fix hasEmail url
     OPTIONAL {?id :hasEmail ?email .
     BIND (iri(replace(str(?email), "%40", "@")) as ?hasEmail)}    
     
     # fix hasTelephone url
     OPTIONAL{?id :hasTelephone ?telephone.
     BIND(iri(replace(str(?telephone), "%2B", "+")) as ?hasTelephone)}
     
     
# Opphav     
     
     OPTIONAL {?id :generertAv ?wasGeneratedBy . }
     OPTIONAL {?id :opphavsKommentar ?provenanceComment . }
     
# Location

     OPTIONAL {
        ?id :geo_uri ?geo_uri_string . # trick to replace URI
        ?id :geo_navn ?gn_name . 
        ?id :geo_navn_no ?gn_name_no .
        ?id :geo_navn_en ?gn_name_en .
        BIND(strlang(?gn_name_no, "no") as ?gn_officialName_no)
        BIND(strlang(?gn_name_en, "en") as ?gn_officialName_en)
        BIND((gn:Feature) as ?gnFeatureClass)
        BIND(iri(?geo_uri_string) as ?geoIRI)
     }
    
# AccessRights
    OPTIONAL {
        ?id :tilgangsrettighet_no ?tilgangsrettighet_no .
        ?id :tilgangsrettighet_en ?tilgangsrettighet_en .
        BIND(strlang(?tilgangsrettighet_no, "no") as ?accessRightNo)
        BIND(strlang(?tilgangsrettighet_en, "en") as ?accessRightEn)        
    }
    
    
    # Make sure the subject is correct
    BIND(if (bound(?geoIRI), ?geoIRI, ?id) as ?s)
}
