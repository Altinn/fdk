prefix apf: <http://jena.apache.org/ARQ/property#>
prefix dcat: <http://www.w3.org/ns/dcat#>
prefix dct:  <https://purl.org/dc/terms/>
prefix : <http://data.brreg.no/vocab/temp/> 
prefix foaf:  <http://xmlns.com/foaf/0.1/>
prefix adms: <http://www.w3.org/ns/adms#> 
prefix dct: <http://purl.org/dc/terms/>
prefix time: <http://www.w3.org/TR/owl-time/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix vcard: <http://www.w3.org/2006/vcard/ns#> 
prefix dcatno: <http://difi.no/dcatno#>  
prefix prov: <http://www.w3.org/ns/prov#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix gn: <http://www.geonames.org/ontology#>
prefix skos: <http://www.w3c.org/2004/02/skos/core#> 

construct {
    ?id a dcat:Dataset .
    ?id dct:identifier ?identifier .
    ?id dct:title ?title_nb .
    ?id dct:title ?title_en .
    ?id dct:description ?description_nb .
    ?id dct:description ?description_en .
    ?id dct:publisher ?publisher .
    ?id dcat:theme ?theme .
    ?id dct:provenance ?provenanceIRI .
    ?id dcat:keyword ?keyword_no .
    ?id dct:subject ?begrepIRI .
    ?id dct:accessRights ?accessRights .
    ?id dct:spatial ?spatialIRI .
    ?id dcatno:accessRightsComment ?skjermingshjemmelIRI .
    ?id dct:accrualPeriodicity ?accrualPeriodicity .
    ?id dcat:contactPoint ?contactPoint .
    ?id dct:conformsTo ?conformsToIRI .
    ?id foaf:page ?pageIRI .
    ?id adms:sample ?sampleIRI .
    ?id dct:source ?source .
    ?id dct:landingPage ?landingPageIRI .
    ?id dct:language ?language .
    ?id adms:identifier ?alternativeIdentifier .
    ?id dct:issued ?issued .
    ?id dct:modified ?modified .
    ?id dcat:distribution ?distribution .

}

WHERE {
 
     # add identifier (ok)
     OPTIONAL {?id :identifikator ?identifier .}

     # add language to title (ok)
     OPTIONAL {?id :tittel_no ?title .
     BIND (STRLANG(?title, "nb") as ?title_nb)}
     
     # add language to english title (ok)
     OPTIONAL {?id :tittel_en ?title2 .
     BIND (STRLANG(?title2, "en") as ?title_en)}

     # add language to norwegian description (ok)
     OPTIONAL {?id :beskrivelse_no ?description .
     BIND (STRLANG(?description, "nb") as ?description_nb)}

     # add language to english description (ok)
     OPTIONAL {?id :beskrivelse_en ?description2 .
     BIND (STRLANG(?description2, "en") as ?description_en)}

     # add publisher (ok)
     OPTIONAL {?id :utgiver ?publisher .} 

     # add theme (ok)
     OPTIONAL {?id :tema ?theme .}

     # create IRI of provenance, if exists
     OPTIONAL {?id :provinence ?provenance} . 
     BIND (IRI(?provenance) AS ?provenanceIRI)
    
     # split :keywords into multiple keyword (ok)
     # NOTE THAT THIS CREATES SERERAL INSTCES OF THIS OBJECT, RESULTS IN POSSIBLE 
     # DUPLICATION OF BNODES
     OPTIONAL {?id :keywords ?keywords .
     ?keyword apf:strSplit(?keywords ",") 
     BIND (STRLANG(?keyword, "nb") as ?keyword_no)}
     
     # create IRI of :begrep, if exsist (ok)
     OPTIONAL {?id :begrep ?begrep} .
     BIND (IRI(?begrep) AS ?begrepIRI)
     
     # create IRI of spatial, if exists (ok)    
     OPTIONAL {?id :spatial ?spatial} .
     BIND (IRI(?spatial) AS ?spatialIRI)
     
     # add accessRights (ok)
     OPTIONAL {?id :tilgangsnivå ?accessRights . }

     # create IRI of :skjermingshjemmel, if exsist (ok)
     OPTIONAL {?id :skjermingshjemmel ?skjermingshjemmel} .
     BIND (IRI(?skjermingshjemmel) AS ?skjermingshjemmelIRI)

     # add accrualPeriodicity (ok)
     OPTIONAL {?id :frekvens ?accrualPeriodicity. }

     # add contactPoint (ok)
     OPTIONAL {?id :kontaktpunkt ?contactPoint . }
     
     # create IRI of conformsTo, if exists (ok)
     OPTIONAL {?id :iSamsvarMed ?conformsTo .
     BIND (IRI(replace(?conformsTo, " ", "%20")) AS ?conformsToIRI)}
     
     # create IRI of page, if exists  (ok)
     OPTIONAL {?id :dokumentasjon ?page} .
     BIND (IRI(?page) AS ?pageIRI)    
 
     # create IRI of sample, if exists (ok)
     OPTIONAL {?id :eksempel ?sample} . 
     BIND (IRI(?sample) AS ?sampleIRI)
    
     # create IRI of landingpage, if exists
     OPTIONAL {?id :landingsside ?landingpage} .
     BIND (IRI(?landingpage) AS ?landingPageIRI)
     
     # add language
     OPTIONAL {?id :språk ?language .}
 
     # add alternative identifier
     OPTIONAL {?id :alternativIdentifikator ?alternativeIdentifier . }
    
     # add issued  - convert to xsd:Date
     OPTIONAL {?id :utgivelsesDato ?issuedDate .
     BIND(xsd:date(str(?issuedDate)) as ?issued)}
    
     # add modified 
     OPTIONAL {?id :modifisert ?modified .}
    
     # add distribution
     OPTIONAL {?id :distribusjon ?distribution.}
    
     # add source
     OPTIONAL {?id :kilde ?source . } 
     
   
}
